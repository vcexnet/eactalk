// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        // You need to add the following repository to download the
        // new plugin.
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.1'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath 'com.google.gms:google-services:4.3.5'

        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.0.0-beta01'

        classpath group: 'org.yaml', name: 'snakeyaml', version: '1.25'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://maven.google.com" // Google's Maven repository
        }
    }
}

task importManifest {
    def ManifestFile = new File("${rootDir.getPath()}/packages/Manifest.yml")
    def EnvVersion = System.env['GOMOBILE_IPFS_VERSION']

    rootProject.ext {
        manifest = new org.yaml.snakeyaml.Yaml().load(ManifestFile.newInputStream())
        version = (EnvVersion != null) ? EnvVersion : '0.0.42-dev'
    }
}

task setupAllProjectsRepos(dependsOn: 'importManifest') {
    allprojects {
        repositories {
            google()
            mavenCentral()
            jcenter()
            // If gomobile-ipfs core is available locally, use it
            maven { url "${rootDir.getPath()}/packages/build/android/maven" }
            // If not available locally, download it from Bintray repo
            maven { url rootProject.ext.manifest.global.android.bintray_url }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

